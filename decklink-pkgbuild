# Maintainer: Antoine Lubineau <antoine@lubignon.info>
 
# milahu@gmail.com:
# aur-decklink-sdk-10.3.PKGBUILD
# https://aur.archlinux.org/packages/decklink-sdk/
# copy PKGBUILD file to "/tmp/yaourt-tmp-$USER/aur-decklink-sdk/PKGBUILD"
# * fixed version to 10.3 [10.3-1?]
#   version 10.4-1 requires registration at blackmagicdesign.com
#   before you can download Blackmagic_DeckLink_SDK_10.4-1.zip
# * fixed source path SDK/Linux --> Linux
 
pkgname=decklink-sdk
pkgver=10.3
pkgrel=1
epoch=1
pkgdesc="Blackmagic Design DeckLink SDK"
arch=('i686' 'x86_64')
url="https://www.blackmagicdesign.com/support/family/capture-and-playback"
license=('custom')
depends=('glu' 'mesa' 'qt4')
makedepends=('unzip')
options=('!strip')
source=("http://software.blackmagicdesign.com/SDK/Blackmagic_DeckLink_SDK_${pkgver}.zip")
sha256sums=('7e07867b39ea036e3f9f9cd1b6e9ef627b6a12366e5bb6cf717cb839e7d5b2be')
 
package() {
  cd "$srcdir"
 
  mkdir -p "$pkgdir/usr/src" "$pkgdir/usr/bin"
  cp -a "$srcdir/Blackmagic DeckLink SDK $pkgver/Linux/include" "$pkgdir/usr/src/decklink-sdk"
  [ "$CARCH" = "i686" ] && _arch='i386'
  [ "$CARCH" = "x86_64" ] && _arch='x86_64'
  cd "$srcdir/Blackmagic DeckLink SDK $pkgver/Linux/Samples/bin/${_arch}"
  for util in Capture DeviceList LoopThroughWithOpenGLCompositing OpenGLOutput SignalGenerator TestPattern
  do
    install -D $util "$pkgdir/usr/bin/$util"
  done
}
 
# vim:set ts=2 sw=2 et:
